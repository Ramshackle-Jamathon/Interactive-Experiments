
<!DOCTYPE HTML>
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
  <title>Fractal</title>
  <meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome plugin for IE if installed -->
  <meta http-equiv="Content-Type" content="text/html;charset=utf-8" >
  <link rel="stylesheet" type="text/css" href="libs/nprogress.css" />
  <link rel="stylesheet" type="text/css" href="fractal.css" />
<script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-58181153-1', 'auto');
  ga('send', 'pageview');
    ga('set', 'dimension1', "FractalHigh"); 

</script>
 <script type="x-shader/x-vertex" id="vertexShader">
    varying vec2 vUv;
    void main()
    {

     vUv = uv;
     
        vec4 mvPosition = modelViewMatrix * vec4(position, 1.0 );
        gl_Position = projectionMatrix * mvPosition;

    }
  </script>
  
  <script type="x-shader/x-fragment" id="fragmentShader">
uniform float iGlobalTime;
uniform float iMinimumDistance;
uniform float iNormalDistance;
uniform int iAnaglyph;
uniform int iForms;
uniform int iSpaceFolding;
uniform vec2 iResolution;
uniform samplerCube iChannel0;
uniform vec3 iCamPosition;
uniform vec3 iCamDir;
uniform vec3 iCamUp;

varying vec2 vUv;
// Kaleidoscopic Journey
//
// Mikael Hvidtfeldt Christensen
// @SyntopiaDK
//
// Edited: Joseph Van Drunen
//
// License:
// Creative Commons Attribution
// http://creativecommons.org/licenses/by/3.0/

// Decrease this for better performance
#define Iterations 5
#define MaxSteps 30
#define ColorInterpolateStep 0.4
#define PI 3.141592
#define Scale 2.0
#define FieldOfView 1.0
#define Jitter 0.0
#define FudgeFactor 1.0

#define Ambient 0.28452
#define Diffuse 0.57378
#define Specular 0.07272
#define LightDir vec3(1.0,1.0,-0.65048)
#define LightColor vec3(1.0,0.666667,0.0)
#define LightDir2 vec3(1.0,-0.62886,1.0)
#define LightColor2 vec3(0.596078,0.635294,1.0)

float time = iGlobalTime;

vec2 rotate(vec2 v, float a) {
	return vec2(cos(a)*v.x + sin(a)*v.y, -sin(a)*v.x + cos(a)*v.y);
}

// Two light source + env light
vec3 getLight(in vec3 color, in vec3 normal, in vec3 dir) {
	vec3 lightDir = normalize(LightDir);
	float specular = pow(max(0.0,dot(lightDir,-reflect(lightDir, normal))),20.0); // Phong
	float diffuse = max(0.0,dot(-normal, lightDir)); // Lambertian
	vec3 lightDir2 = normalize(LightDir2);
	float specular2 = pow(max(0.0,dot(lightDir2,-reflect(lightDir2, normal))),20.0); // Phong
	float diffuse2 = max(0.0,dot(-normal, lightDir2)); // Lambertian
	
	return
		 textureCube(iChannel0, reflect(dir, normal)).xyz*Specular+
		(Specular*specular)*LightColor+(diffuse*Diffuse)*(LightColor*color) +
		(Specular*specular2)*LightColor2+(diffuse2*Diffuse)*(LightColor2*color);
}

// Geometric orbit trap. Creates the fractal forms look.
float trap(vec3 p){
    if (iForms == 1) return  length(p.x-0.5-0.5*sin(time/10.0)); // <- cube forms 
    if (iForms == 2) return  length(p.x-1.0); // <-plane forms
    if (iForms == 3) return length(p.xz-vec2(1.0,1.0))-0.05; // <- tube forms
    return length(p); // <- no trap
}

vec3 offset = vec3(1.0+0.2*(cos(time/5.7)),0.3+0.1*(cos(time/1.7)),1.).xzy;

// DE: Infinitely tiled Kaleidoscopic IFS. 
//
// For more info on KIFS, see: 
// http://www.fractalforums.com/3d-fractal-generation/kaleidoscopic-%28escape-time-ifs%29/
float DE(in vec3 z)
{	
	// Folding 'tiling' of 3D or 2D space;
	if (iSpaceFolding == 0)z.xy = abs(1.0-mod(z.xy,2.0))+0.2;
    if (iSpaceFolding == 1) z  = abs(1.0-mod(z,2.0))+0.2;
	
	float d = 1000.0;
	float r;
	for (int n = 0; n < Iterations; n++) {
		z.xz = rotate(z.xz, time/18.0);
		
		// This is octahedral symmetry,
		// with some 'abs' functions thrown in for good measure.
		if (z.x+z.y<0.0) z.xy = -z.yx;
		z = abs(z);
		if (z.x+z.z<0.0) z.xz = -z.zx;
		z = abs(z);
		if (z.x-z.y<0.0) z.xy = z.yx;
		z = abs(z);
		if (z.x-z.z<0.0) z.xz = z.zx;
		z = z*Scale - offset*(Scale-1.0);
		z.yz = rotate(z.yz, -time/18.0);
		
		d = min(d, trap(z) * pow(Scale, -float(n+1)));
	}
	return d;
}

// Finite difference normal
vec3 getNormal(in vec3 pos) {
	vec3 e = vec3(0.0,iNormalDistance,0.0);
    
	return normalize(vec3(
			DE(pos+e.yxx)-DE(pos-e.yxx),
			DE(pos+e.xyx)-DE(pos-e.xyx),
			DE(pos+e.xxy)-DE(pos-e.xxy)));
}

// Solid color with a little bit of normal :-)
vec3 getColor(vec3 normal) {
	return mix(vec3(1.0),abs(normal),ColorInterpolateStep); 
}

// Filmic tone mapping:
// http://filmicgames.com/archives/75
vec3 toneMap(in vec3 c) {
	c = pow(c,vec3(2.0));
	vec3 x = max(vec3(0.0),c-vec3(0.004));
	c = (x*(6.2*x+.5))/(x*(6.2*x+1.7)+0.06);
	return c;
}


// Pseudo-random number
// From: lumina.sourceforge.net/Tutorials/Noise.html
float rand(vec2 co){
	return fract(cos(dot(co,vec2(4.898,7.23))) * 23421.631);
}

vec4 rayMarch(in vec3 from, in vec3 dir, in vec2 pix) {
	// Add some noise to prevent banding
	float totalDistance = Jitter*rand(pix+vec2(time));
	
	float distance = 0.0;
	int steps = 0;
	vec3 pos;
	for (int i=0; i < MaxSteps; i++) {
		pos = from + totalDistance * dir;
		distance = DE(pos)*FudgeFactor;
		totalDistance += distance;
		if (distance < iMinimumDistance){ break; }
		steps = i;
	}
	
	// 'AO' is based on number of steps.
	// Try to smooth the count, to combat banding.
	float smoothStep = float(steps) + float(distance/iMinimumDistance);
	float temp = (smoothStep/float(MaxSteps));
	//float ao = 1.0 - abs(temp);
	float ao = 1.0 - clamp(temp, 0.0, 1.0);
	

	// Since our distance field is not signed,
    // backstep when calc'ing normal
	vec3 normal = getNormal(pos-dir*iNormalDistance*3.0);
	vec3 color = getColor(normal);
	vec3 light = getLight(color, normal, dir);
	
	
	
	
	return vec4(toneMap((color*Ambient+light)*ao),1.0);
}







void main(void)
{
	
    // first texture row is frequency data
	//float fft  = texture2D( iChannel1, vec2(vUv.x,0.25) ).x; 
	
    // second texture row is the sound wave
	//float wave = texture2D( iChannel1, vec2(vUv.x,0.75) ).x;
	
    vec2 coord =(-1.0 + 2.0 *vUv);
	coord.x *= iResolution.x/iResolution.y;
	
	bool isCyan;
	if(iAnaglyph == 1){
	    isCyan = 0.5<mod(gl_FragCoord.xy.x,2.0);
	    if(.5<mod(gl_FragCoord.xy.y,2.0)){ isCyan = !isCyan; }
	}
	
	
	// Camera position (eye), and camera target
	vec3 camPos = vec3(iCamPosition.x,iCamPosition.y,iCamPosition.z);
	if(iAnaglyph == 1){
	    if(isCyan)camPos = vec3(iCamPosition.x + 0.05,iCamPosition.y,iCamPosition.z);
	}
    vec3 target = camPos+vec3(iCamDir.x,iCamDir.y,iCamDir.z);
	vec3 camUp  = vec3(iCamUp.x,iCamUp.y,iCamUp.z);
	
	// Calculate orthonormal camera reference system
	vec3 camDir   = normalize(target-camPos); // direction for center ray
	camUp = normalize(camUp-dot(camDir,camUp)*camDir); // orthogonalize
	vec3 camRight = normalize(cross(camDir,camUp));
	
	
	// Get direction for this pixel
	vec3 rayDir = normalize(camDir + (coord.x*camRight + coord.y*camUp)*FieldOfView);
    
    vec4 col = rayMarch(camPos, rayDir, gl_FragCoord.xy );
    
    gl_FragColor = col;
    if(iAnaglyph == 1){
        if(isCyan)
	        gl_FragColor=vec4(0.0,col.gb,1.0);
        else
	        gl_FragColor=vec4(sqrt(col.r),0.0,0.0,1.0);
    }
    
}
  </script>  
 
</head>

<body>

<div id="container">

    <div id="messageWindow">
        <div id="messages"></div>
    </div>
</div>

<!-- javascript, loaded at the end to make the page appear faster -->
<script type="text/javascript" src="https://ajax.googleapis.com/ajax/libs/jquery/1.11.2/jquery.min.js"></script>
<script type="text/javascript" src="libs/three.min.js"></script>
<script type="text/javascript" src="libs/FlyControls.js"></script>
<script type="text/javascript" src="libs/Detector.js"></script>
<script type="text/javascript" src="libs/stats.min.js"></script>
<script type="text/javascript" src="libs/dat.gui.min.js"></script>
<script type="text/javascript" src="libs/nprogress.js"></script>
<script type="text/javascript" src="fractal.js"></script>

</body>
</html>




